"""core/___prelude.rock
"""

class Iter {
  var more, next

  def __init(more, next) {
    this.more = more
    this.next = next
  }

  def iter() = this

  def more() = (this.more)()
  def next() = (this.next)()

  def each(f) =
    while this.more() {
      f(this.next())
    }

  def map(f) =
    Iter(\. this.more(), \. f(this.next()))

  def to(klass) = klass.fromIter(this)
}

class Range {
  var start, end

  def __init(start, end) {
    this.start = start
    this.end = end
  }

  def iter() {
    var i = this.start
    Iter(
      \. i < this.end,
      \ {
        i = i + 1
        i - 1
      })
  }

  def __str() =
    "Range(" + this.start.to(String) + ", " + this.end.to(String) + ")"

  def to(klass) =
    switch klass {
      case List: this.iter().to(klass)
      default: super.to(klass)
    }
}
__declare('Range', Range)
